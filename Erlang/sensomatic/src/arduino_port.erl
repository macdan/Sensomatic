-module( arduino_port ).

-export( [ 
	start_link/1,
	set_value/2, 
	get_value/1,
	add_handler/3,
	get_spec/1,
	commit/1
] ).

-behaviour( gen_server ).
-export( [ 
	init/1, 
	handle_call/3, 
	handle_cast/2, 
	handle_info/2, 
	code_change/3, 
	terminate/2 
] ).

-record( state, { 
	device, 
	event, 
	id, 
	rw, 
	type, 
	value 
} ).

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% module api
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%%==============================================================================
%% start_link/1
%%==============================================================================
start_link( PortSpec ) ->
	gen_server:start_link( ?MODULE, [ PortSpec ], [] ).

%%==============================================================================
%% set_value/2
%%==============================================================================
set_value( Pid, Value ) -> 
	gen_server:call( Pid, { set_value, Value } ).
	
%%==============================================================================
%% get_value/1
%%==============================================================================
get_value( Pid ) -> 
	gen_server:call( Pid, get_value ).

get_spec( Pid ) ->
	gen_server:call( Pid, get_spec ).

%%==============================================================================
%% add_handler/3
%%==============================================================================
add_handler( Pid, Handler, Args ) ->
	gen_server:call( Pid, { add_handler, Handler, Args } ).
	
commit( Pid ) ->
	gen_server:cast( Pid, commit ).

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% gen_server callbacks
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%%==============================================================================
%% init/1
%%==============================================================================
init( [ { DevicePid, Id, Rw, Type } ] ) ->
	util:shout( "Starting ~p port ~p on device ~p...", [ Id, Type, DevicePid ] ),
	{ ok, EventPid } = gen_event:start_link(),
	{ ok, #state{ 
		device = DevicePid,
		id = Id,
		rw = Rw,
		type = Type,
		event = EventPid
	} }.

%%==============================================================================
%% handle_cast/2
%%==============================================================================
%% Commit
%%------------------------------------------------------------------------------
handle_cast( commit, State ) ->
	device:commit( State#state.device ),
	{ noreply, State };
%%------------------------------------------------------------------------------
%% Catch All
%%------------------------------------------------------------------------------
handle_cast( Cast, State ) ->
	util:shout( "unexpected cast: ~p", [ Cast ] ),
    { noreply, State }.

%%==============================================================================
%% handle_info/2
%%==============================================================================
handle_info( Msg, State ) ->
    util:shout( "unexpected: ~p", [ Msg ] ),
    { noreply, State }.

%%==============================================================================
%% handle_call/2
%%==============================================================================
%% Get Spec
%%------------------------------------------------------------------------------
handle_call( get_spec, _, State ) ->
	{ reply, { State#state.device, State#state.id, State#state.rw, State#state.type }, State };
%%------------------------------------------------------------------------------
%% Get Value
%%------------------------------------------------------------------------------
handle_call( get_value, _From, State ) ->
	{ reply, State#state.value, State };
%%------------------------------------------------------------------------------
%% Set Value
%%------------------------------------------------------------------------------
handle_call( { set_value, Value }, _From, State ) when is_integer( Value ) ->
	if
		Value =/= State#state.value ->
			gen_event:notify( State#state.event, { value_changed, self(), Value } );
		Value =:= State#state.value ->
			ok
	end,
	{ reply, ok, State#state{ value = Value } };

%%------------------------------------------------------------------------------
%% Add Handler 
%%------------------------------------------------------------------------------
handle_call( { add_handler, Handler, Args }, _, State ) ->
	{ reply,
		gen_event:add_handler( State#state.event, Handler, Args ),
	State };
%%------------------------------------------------------------------------------
%% Catch All
%%------------------------------------------------------------------------------
handle_call( Call, _From, State ) ->
	util:shout( "unexpected call: ~p", [ Call ] ),
	{ noreply, State }.

%%==============================================================================
%% code_change/3
%%==============================================================================
code_change( _OldVsn, State, _Extra ) ->
	{ ok, State }.

%%==============================================================================
%% terminate/2
%%==============================================================================
terminate( normal, _State ) ->
	ok;
terminate( Reason, _State ) ->
	util:shout( "terminate reason: ~p~n", [ Reason ] ).

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% private functions
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
